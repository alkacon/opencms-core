import org.apache.tools.ant.filters.ReplaceTokens
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = java_target_version
targetCompatibility = java_target_version
buildDir = build_directory

// use the same properties file as the OpenCms core does for the version information
// please note that in a Jenkins CI build, this properties file should be dynamically generated
def externalVersionProps = file("${buildDir}/../version.properties");
def propFile;
if (externalVersionProps.exists()){
	println "Using external version properties file: $externalVersionProps"
	propFile=externalVersionProps
}else{
	println 'Using internal version properties file'
	propFile=file("$projectDir/src/org/opencms/main/version.properties")
}

Properties vprops = new Properties()
vprops.load(new FileInputStream(propFile))
version = vprops['version.number']

// alternative variable name for the case where the version variable is used for something else, e.g. javadoc
ext.ocmsVersion = version

repositories {
    mavenLocal()
    if (project.hasProperty('additional_repositories')){
        additional_repositories.split(';').each{ repo ->
            maven { url repo }
        }
    }
    jcenter()
    maven {
        url "http://maven.vaadin.com/vaadin-addons"
    }
}

if (!project.hasProperty('max_heap_size')){
    project.ext.max_heap_size='1024m'
}

Properties props = new Properties()
props.load(new FileInputStream("$projectDir/modules/all-modules.properties"))
project.ext.allModuleNames = props['modules.common.all']
project.ext.allExternalModuleNames = ''
project.ext.modulesDistsDir = file("${project.buildDir}/modulesZip")
def altExternalStructure = false
if (project.hasProperty('external_directories')){
    def externalPropFile =file(external_directories+'/modules/all-modules.properties')
    if (externalPropFile.exists()){
        Properties externalprops = new Properties()
        println "reading external modules from ${external_directories}/modules/all-modules.properties"
        externalprops.load(new FileInputStream(externalPropFile))
        project.ext.allExternalModuleNames = externalprops['modules.common.all']
        project.ext.allModuleNames += ','+allExternalModuleNames
    }else{
    	externalPropFile =file(external_directories+'/all-modules.properties')
    	if (externalPropFile.exists()){
    		Properties externalprops = new Properties()
        	println "reading external modules from ${external_directories}/all-modules.properties"
        	externalprops.load(new FileInputStream(externalPropFile))
        	project.ext.allExternalModuleNames = externalprops['modules.common.all']
        	project.ext.allModuleNames += ','+allExternalModuleNames
        	altExternalStructure = true
    	}else{
        	println "Can\'t read from external module properties from directory $external_directories"
        }
    }
}

configurations {

    distribution {
        description = 'libraries included into the opencms distribution'
        transitive = false
    }

    compile {
        description = 'used to compile the opencms.jar and the modules jars'
        transitive = false
        extendsFrom distribution
    }

    modulesCompile {
        description = 'used to compile the modules classes'
        transitive = false
        extendsFrom compile
    }

    testCompile{
        transitive = false
        extendsFrom modulesCompile
    }

    testRuntime{ transitive = false }

    gwtCompile {
        description = 'needed to generate the GWT JavaScript resources'
        transitive = false
        extendsFrom modulesCompile
    }

    testGwtCompile{
        description = 'needed to run GWT test cases'
        transitive = false
        extendsFrom gwtCompile
    }

    testGwtRuntime{ transitive = false }

    setupCompile {
        transitive = false
        extendsFrom modulesCompile
    }
}

configurations.all {
    transitive = false;
}

// import dependencies
apply from: 'dependencies.gradle'

sourceSets{
    main {
        java.srcDirs=['src']
        resources.srcDirs=['src']
    }
    modules {
        java.srcDirs=['src-modules']
        resources.srcDirs=['src-modules']
    }
    gwt {
        java.srcDirs=['src-gwt']
        resources.srcDirs=['src-gwt']
    }
    setup {
        java.srcDirs=['src-setup']
        resources.srcDirs=['src-setup']
    }
    test {
        java.srcDirs=['test']
        resources.srcDirs=['test','src','src-modules','src-setup']
    }

    testGwt {
        java.srcDirs=['src-gwt','test-gwt']
        resources.srcDirs=['src-gwt','test-gwt']
    }
}

// adding generated dependencies during configuration phase to avoid build path problems in IDE
sourceSets.modules.compileClasspath += files("$buildDir/classes/main") { builtBy 'compileJava' }
sourceSets.setup.compileClasspath += files("$buildDir/classes/main") { builtBy 'compileJava' }
sourceSets.gwt.compileClasspath += files("$buildDir/classes/main") { builtBy 'compileJava' }
sourceSets.gwt.compileClasspath += files("$buildDir/classes/modules") { builtBy 'modulesClasses' }
sourceSets.test.compileClasspath += fileTree('webapp/setup/database'){ include '**/*.jar' }
sourceSets.test.compileClasspath += files("$buildDir/classes/setup") { builtBy 'setupClasses' }
sourceSets.test.compileClasspath += files("$buildDir/classes/modules") { builtBy 'modulesClasses' }
sourceSets.test.compileClasspath += files("$buildDir/classes/gwt") { builtBy 'gwtClasses' }
sourceSets.testGwt.compileClasspath += files("$buildDir/classes/main") { builtBy 'compileJava' }
sourceSets.testGwt.compileClasspath += files("$buildDir/classes/modules") { builtBy 'modulesClasses' }

task copyDeps(type: Copy) {
    from configurations.distribution
    into "${buildDir}/deps"
}

task copyCompileDeps(type: Copy) {
	from configurations.compile
	into "${buildDir}/deps"
}

task enhance << {
    ant.taskdef(
    name      : 'openjpaenhancer',
    classpath : sourceSets.modules.compileClasspath.asPath,
    classname : 'org.apache.openjpa.ant.PCEnhancerTask'
    )

    ant.openjpaenhancer(classpath : sourceSets.modules.compileClasspath.asPath){
        fileset(dir: "${sourceSets.main.output.classesDir}/org/opencms/db/jpa/persistence"){ exclude(name: '*$*.class') }
        config(propertiesFile: "${projectDir}/webapp/WEB-INF/classes/META-INF/persistence.xml")
    }
}

jar {
    it.dependsOn enhance
    manifest {
        attributes 'Implementation-Title': 'Alkacon OpenCms',
        'Implementation-Version': version,
        'JPA-PersistenceUnits': 'DATA_OPENJPA,DATA_OPENJPA_JTA',
        'Meta-Persistence': 'META-INF/persistence.xml'
    }
    baseName 'opencms-core'
    archiveName 'opencms.jar'
    
    include 'org/opencms/**'
    include 'org/vaadin/risto/**'
    include 'com/**'
    include 'META-INF/**'
    exclude '**/package.html'
    exclude '**/overview.html'
    exclude '**/.gitignore'
    
    if (externalVersionProps.exists()){
        exclude 'org/opencms/main/version.properties'
        from ("${buildDir}/.."){
            include 'version.properties'
            into 'org/opencms/main'
        }
    }

    metaInf {
        from("${projectDir}/webapp/WEB-INF/opencms.tld")
    }

    doLast {
       if (project.hasProperty('tomcat_update_target') && file(tomcat_update_target).exists()){
            println "copying ${project.buildDir}/libs/${archiveName} to ${tomcat_update_target}"
            copy {
                from  "${project.buildDir}/libs/${archiveName}"
                into tomcat_update_target
            }
        }
    }
}

javadoc {
    doLast {
        copy {
            from  "${projectDir}/doc/javadoc/logos"
            into "${buildDir}/docs/javadoc/logos"
        }
    }

    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        author="true"
        version="true"
        source= java_target_version
        linkSource="false"
        overview="${sourceSets.main.output.resourcesDir}/org/opencms/main/overview.html"
        docTitle="OpenCms Core API, version $ocmsVersion"
        windowTitle="OpenCms Core API, version $ocmsVersion"
        stylesheetFile= file("${projectDir}/doc/javadoc/stylesheet.css")
        header = "<script type=\"text/javascript\"> if (window.location.href.indexOf(\"overview-frame\") == -1) { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.alkacon.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/Alkacon.png\\\" /></a>\"); } else { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.opencms.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/OpenCms.png\\\" /></a>\"); }</script>"
        use="true"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadoc"
    baseName 'opencms-core'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    baseName 'opencms-core'
}

task gwtJar(dependsOn: jar, type: Jar) {
    from sourceSets.gwt.output
    from (sourceSets.gwt.allSource){ include '**/*.java' }
    from (sourceSets.main.allSource){
        include '**/shared/**'
        include '**/gwt/CmsRpcException.java'
        include '**/ade/detailpage/CmsDetailPageInfo.java'
        include '**/db/CmsResourceState.java'
        include '**/jsp/CmsContainerJsonKeys.java'
        include '**/util/CmsPair.java'
        include '**/util/CmsDefaultSet.java'
        include '**/xml/content/CmsXmlContentProperty.java'
        include '**/workplace/editors/CmsTinyMceToolbarHelper.java'
    }
    includeEmptyDirs false
    archiveName 'opencms-gwt.jar'
    baseName 'opencms-gwt'
    exclude '**/.gitignore'
}

task javadocModules(type: Javadoc, dependsOn: jar) {
    doLast {
        copy {
            from  "${projectDir}/doc/javadoc/logos"
            into "${buildDir}/docs/javadocModules/logos"
        }
    }

    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        author="true"
        version="true"
        source= java_target_version
        linkSource="false"
        docTitle="OpenCms Workplace Modules API, version $ocmsVersion"
        windowTitle="OpenCms Workplace Modules API, version $ocmsVersion"
        stylesheetFile= file("${projectDir}/doc/javadoc/stylesheet.css")
        header = "<script type=\"text/javascript\"> if (window.location.href.indexOf(\"overview-frame\") == -1) { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.alkacon.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/Alkacon.png\\\" /></a>\"); } else { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.opencms.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/OpenCms.png\\\" /></a>\"); }</script>"
    }
    source = sourceSets.modules.allJava
    classpath = project.sourceSets.modules.compileClasspath
    destinationDir = file("${buildDir}/docs/javadocModules")
}

task javadocJarModules(type: Jar, dependsOn: javadocModules) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadocModules"
    baseName 'opencms-modules'
}

task sourcesJarModules(type: Jar) {
    from sourceSets.modules.allSource
    classifier = 'sources'
    baseName 'opencms-modules'
}

task javadocGwt(type: Javadoc, dependsOn: jar) {
    doLast {
        copy {
            from  "${projectDir}/doc/javadoc/logos"
            into "${buildDir}/docs/javadocGwt/logos"
        }
    }

    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        author="true"
        version="true"
        source= java_target_version
        linkSource="false"
        docTitle="OpenCms GWT Components API, version $ocmsVersion"
        windowTitle="OpenCms GWT Components API, version $ocmsVersion"
        stylesheetFile= file("${projectDir}/doc/javadoc/stylesheet.css")
        header = "<script type=\"text/javascript\"> if (window.location.href.indexOf(\"overview-frame\") == -1) { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.alkacon.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/Alkacon.png\\\" /></a>\"); } else { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.opencms.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/OpenCms.png\\\" /></a>\"); }</script>"
    }
    source = sourceSets.gwt.allJava
    classpath = project.sourceSets.gwt.compileClasspath
    destinationDir = file("${buildDir}/docs/javadocGwt")
}

task javadocJarGwt(type: Jar, dependsOn: javadocGwt) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadocGwt"
    baseName 'opencms-gwt'
}

task sourcesJarGwt(type: Jar) {
    from sourceSets.gwt.allSource
    from (sourceSets.main.allSource){
        include '**/shared/**'
        include '**/gwt/CmsRpcException.java'
        include '**/ade/detailpage/CmsDetailPageInfo.java'
        include '**/db/CmsResourceState.java'
        include '**/jsp/CmsContainerJsonKeys.java'
        include '**/util/CmsPair.java'
        include '**/util/CmsDefaultSet.java'
        include '**/xml/content/CmsXmlContentProperty.java'
        include '**/workplace/editors/CmsTinyMceToolbarHelper.java'
    }
    includeEmptyDirs false
    classifier = 'sources'
    baseName 'opencms-gwt'
}

task workplaceTheme (type: JavaExec) {
    doFirst {
        println '======================================================'
        println "Building workplace theme"
        println "Generating $project.buildDir/workplaceThemes/opencms/styles.css"
        println '======================================================'
        def dir = file("$project.buildDir/workplaceThemes/opencms")
        if (dir.exists()){
            delete(dir)
        }
        dir.mkdirs()
    }

    main = 'com.vaadin.sass.SassCompiler'

    classpath {
        [
            sourceSets.main.compileClasspath
 
        ]
    }


    args = [
        "$projectDir/webapp/workplaceThemes/VAADIN/themes/opencms/styles.scss",
        "$project.buildDir/workplaceThemes/opencms/styles.css"
    ]
    maxHeapSize = max_heap_size
}

task "gwt_widgetSet" (dependsOn: gwtClasses, type: JavaExec) {
    ext.buildDir =  project.buildDir.toString()  +"/gwt/widgetSet"
    ext.extraDir =  project.buildDir.toString() + "/extra/widgetSet"
    inputs.source sourceSets.gwt.java.srcDirs
    inputs.dir sourceSets.gwt.output.resourcesDir
    outputs.dir buildDir

    // Workaround for incremental build (GRADLE-1483)
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()

    doFirst {
        println '======================================================'
        println "Building GWT resources for WidgetSet"
        println '======================================================'
        // to clean the output directory, delete it first
        def dir = file(buildDir)
        if (dir.exists()){
            delete(dir)
        }
        dir.mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
            // Java source core
            sourceSets.main.java.srcDirs,
            // Java source gwt
            sourceSets.gwt.java.srcDirs,
            // Java source modules
            sourceSets.modules.java.srcDirs,
            // Generated resources
            sourceSets.gwt.output.resourcesDir,
            // Generated classes
            sourceSets.gwt.output.classesDir,
            // Dependecies
            sourceSets.gwt.compileClasspath,
        ]
    }


    args =
            [
                'org.opencms.ui.WidgetSet',
                // Your GWT module
                '-war',
                buildDir,
                '-logLevel',
                'ERROR',
                '-localWorkers',
                '2',
                '-style',
                'obfuscated',
                '-extra',
                extraDir,
                '-strict'
                // '-draftCompile' // Speeds up compile with 25%
            ]
    jvmArgs = [ '-Dgwt.jjs.permutationWorkerFactory=com.google.gwt.dev.ThreadedPermutationWorkerFactory']
    

    maxHeapSize = max_heap_size
}

task widgetSetJar(type: Jar, dependsOn: [gwt_widgetSet, workplaceTheme]){
    doFirst {
        println '======================================================'
        println "Building WidgetSet Jar"
        println "including resources from ${project.buildDir}/gwt/widgetSet"
        println '======================================================'
    }
    baseName='opencms-widgetset'
    archiveName='opencms-widgetset.jar'
    from ("${projectDir}/webapp/workplaceThemes"){
        exclude '**/*.scss'
    }
    from ("${project.buildDir}/workplaceThemes/"){
        into 'VAADIN/themes'
    }
    from ("${project.buildDir}/gwt/widgetSet"){
        include 'org.opencms.ui.WidgetSet/**'
        into 'VAADIN/widgetsets'
    }
    
    doLast {
       if (project.hasProperty('tomcat_update_target') && file(tomcat_update_target).exists()){
            println "copying ${project.buildDir}/libs/${archiveName} to ${tomcat_update_target}"
            copy {
                from  "${project.buildDir}/libs/${archiveName}"
                into tomcat_update_target
            }
        }
    }
}


task setupJar(dependsOn: jar, type: Jar) {
    from sourceSets.setup.output
    archiveName 'opencms-setup.jar'
    baseName 'opencms-setup'
    exclude '**/.gitignore'
}

task javadocSetup(type: Javadoc, dependsOn: jar) {
    source = sourceSets.setup.allJava
    classpath = project.sourceSets.setup.compileClasspath
    destinationDir = file("${buildDir}/docs/javadocSetup")
    doLast {
        copy {
            from  "${projectDir}/doc/javadoc/logos"
            into "${buildDir}/docs/javadocSetup/logos"
        }
    }
    
    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        author="true"
        version="true"
        source= java_target_version
        linkSource="false"
        docTitle="OpenCms Setup API, version $ocmsVersion"
        windowTitle="OpenCms Setup API, version $ocmsVersion"
        stylesheetFile= file("${projectDir}/doc/javadoc/stylesheet.css")
        header = "<script type=\"text/javascript\"> if (window.location.href.indexOf(\"overview-frame\") == -1) { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.alkacon.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/Alkacon.png\\\" /></a>\"); } else { document.write(\"<a id=\\\"brandingLink\\\" target=\\\"_blank\\\" href=\\\"http://www.opencms.com\\\"><img border=\\\"0\\\" id=\\\"brandingPic\\\" src=\\\"{@docRoot}/logos/OpenCms.png\\\" /></a>\"); }</script>"
    }
}

task javadocJarSetup(type: Jar, dependsOn: javadocSetup) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadocSetup"
    baseName 'opencms-setup'
}

task sourcesJarSetup(type: Jar) {
    from sourceSets.setup.allSource
    classifier = 'sources'
    baseName 'opencms-setup'
}

def preserveLibModules = ''

// iterate all available modules and create the required tasks
allModuleNames.split(',').each{ moduleName ->
    def modulePackages = moduleName
    def gwtModule = null
    def moduleFolder = file("${projectDir}/modules/${moduleName}")
    def srcGwtDir = "${projectDir}/src-gwt/"
    def moduleSrcFolder = file("${projectDir}/src-modules")
    def isExternal = allExternalModuleNames.contains(moduleName)

    if (isExternal.toBoolean()){
    	if (altExternalStructure.toBoolean()){
    		println "configuring alt external module ${moduleName}"
    		moduleFolder = file("${external_directories}/${moduleName}")
        	srcGwtDir = file("${moduleFolder}/src-gwt/")
        	moduleSrcFolder = file("${moduleFolder}/src")
    	}else{
    		println "configuring alt external module ${moduleName}"
    		moduleFolder = file("${external_directories}/modules/${moduleName}")
        	srcGwtDir = file("${external_directories}/src-gwt/")
        	moduleSrcFolder = file("${external_directories}/src")
        }
    }
    def propertyFile = file("${moduleFolder}/module.properties")
    def gwtRename = null
    if (propertyFile.exists()){
        Properties moduleProperties= new Properties()
        moduleProperties.load(new FileInputStream(propertyFile))
        if (moduleProperties['module.packages']!=null){
            modulePackages=moduleProperties['module.packages']
        }
        if (moduleProperties['module.gwt']!=null){
            gwtModule = moduleProperties['module.gwt']
            def moduleXml = (new XmlParser()).parse(srcGwtDir.toString()+"/" +gwtModule.replaceAll('\\.','/')+'.gwt.xml')
            gwtRename = moduleXml['@rename-to']
            if (gwtRename==null){
                gwtRename=gwtModule
            }
        }
    }
    def manifestFile = file("${moduleFolder}/resources/manifest.xml")
    def moduleDependencies=[]
    def moduleVersion = version
    if (manifestFile.exists()){
        def parsedManifest= (new XmlParser()).parse("${moduleFolder}/resources/manifest.xml")
        parsedManifest.module[0].dependencies[0].dependency.each{ dep ->
            moduleDependencies.add(dep.@name)
        }
        moduleVersion = parsedManifest.module[0].version[0].text()
    }
	def staticFolder=file("${moduleFolder}/static")
    def requiresJar = gwtModule!=null || staticFolder.exists()
    if (!requiresJar){
	    // check if there are any java packages to include into the module's jar
	    modulePackages.split(',').each{ pack ->
	        def packageFolder
	        packageFolder= file("${moduleSrcFolder}/"+ pack.replaceAll('\\.','/'))
	        if (packageFolder.exists()&&packageFolder.isDirectory()){
	        	packageFolder.listFiles().each{ file ->
	                if (file.isFile()){
	                    requiresJar = true
	                }
	            }
	        }
	    }
    }

	if (requiresJar){
		preserveLibModules += "${moduleName},"
	}

    task "dist_$moduleName" (type: Zip){
        ext.moduleName = moduleName
        ext.moduleFolder = moduleFolder
        ext.isExternal = isExternal
        ext.requiresJar = requiresJar
        ext.dependencies = moduleDependencies
        
        if (project.hasProperty('noVersion')) {
        	version ""
        } else {
         	version moduleVersion
        }
        destinationDir modulesDistsDir
        // include the jar file if necessary
        if (requiresJar.toBoolean()&&isExternal.toBoolean()){
        	from( "${project.buildDir}/libs") {
                include "${moduleName}.jar"
                into "/system/modules/${moduleName}/lib/"
            }
        }
        from "${moduleFolder}/resources"

        // TODO: set the module version to match the manifest.xml
        baseName moduleName
        doFirst {
            println '======================================================'
            println "Building ZIP for $moduleName version $moduleVersion"
            println '======================================================'
        }
       doLast {
       		if (project.hasProperty('module_copy_target') && file(module_copy_target).exists()){
            	println "copying ${project.buildDir}/modulesZip/${archiveName} to ${module_copy_target}"
            	copy {
                	from  "${project.buildDir}/modulesZip/${archiveName}"
                	into module_copy_target
            	}
            }
	    }
        
    }

    if (requiresJar.toBoolean()){

        sourceSets.create(moduleName)
        sourceSets[moduleName].java.srcDirs=files("$moduleSrcFolder")
        sourceSets[moduleName].resources.srcDirs=files("$moduleSrcFolder")
        modulePackages.split(',').each{ pack ->
            sourceSets[moduleName].java.includes.add(pack.replaceAll('\\.','/')+'/*')
            sourceSets[moduleName].resources.includes.add(pack.replaceAll('\\.','/')+'/*')
        }

        sourceSets[moduleName].compileClasspath=sourceSets.modules.compileClasspath
        def libFolder=file("${moduleFolder}/resources/system/modules/${moduleName}/lib")
        if (libFolder.exists()){
        	println("Detecting lib folder for module $moduleName")
        	sourceSets[moduleName].compileClasspath+= fileTree(libFolder){ include '**/*.jar' }
        	
        }

        task "jar_$moduleName" (type: Jar) {
        	ext.gwtModule = gwtModule
        	ext.moduleName = moduleName
        	manifest {
                attributes 'Implementation-Title': 'Alkacon OpenCms', 'Implementation-Version': version
            }
            from sourceSets[moduleName].output
            from ("$moduleFolder") { include "META-INF/**" }
            from ("$staticFolder") { into "OPENCMS" }
            if (gwtModule != null){
	            from( "${project.buildDir}/gwt/${moduleName}") {
	                exclude '**/WEB-INF/**'
	                into "OPENCMS/gwt"
	            }
            }
            archiveName moduleName+'.jar'
            baseName moduleName
            exclude '**/.gitignore'
            doFirst{
                println '======================================================'
                println "Building ${moduleName}.jar including $modulePackages"
                println '======================================================'
            }

            doLast {
                if (project.hasProperty('tomcat_update_target') && file(tomcat_update_target).exists()){
                    println "copying ${project.buildDir}/libs/${archiveName} to ${tomcat_update_target}"
                    copy {
                        from  "${project.buildDir}/libs/${archiveName}"
                        into tomcat_update_target
                    }
                }
            }
        }

        task "javadoc_$moduleName" (type: Javadoc){
            source = sourceSets[moduleName].allJava
            classpath = project.sourceSets[moduleName].compileClasspath
            destinationDir = file("${buildDir}/docs/javadoc${moduleName}")
            
        }

        task "javadocJar_$moduleName"(type: Jar) {
            it.dependsOn("javadoc_$moduleName")
            classifier = 'javadoc'
            from "${buildDir}/docs/javadoc${moduleName}"
            baseName moduleName
        }

        task "sourcesJar_$moduleName"(type: Jar) {
            from sourceSets[moduleName].allSource
            classifier = 'sources'
            baseName moduleName
        }

      	if (isExternal.toBoolean()){
      		tasks["dist_$moduleName"].dependsOn("jar_$moduleName")
      	}
    }

    if (gwtModule != null){
        sourceSets.create(moduleName+'_gwt')
        sourceSets[moduleName+'_gwt'].java.srcDirs=files("$srcGwtDir")
        sourceSets[moduleName+'_gwt'].resources.srcDirs=files("$srcGwtDir")
        task "gwt_$moduleName" (dependsOn: gwtClasses, type: JavaExec) {
            ext.buildDir =  project.buildDir.toString()  +"/gwt/$moduleName"
            ext.extraDir =  project.buildDir.toString() + "/extra/$moduleName"
            inputs.source sourceSets.gwt.java.srcDirs
            inputs.dir sourceSets.gwt.output.resourcesDir
            outputs.dir buildDir

            // Workaround for incremental build (GRADLE-1483)
            outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()

            doFirst {
                println '======================================================'
                println "Building GWT resources for $gwtModule"
                println '======================================================'
                // to clean the output directory, delete it first
                def dir = file(buildDir)
                if (dir.exists()){
                    delete(dir)
                }
                dir.mkdirs()
            }

            main = 'com.google.gwt.dev.Compiler'

            classpath {
                [
                    // Java source core
                    sourceSets.main.java.srcDirs,
                    // Java source gwt
                    sourceSets.gwt.java.srcDirs,
                    // Java source modules
                    sourceSets.modules.java.srcDirs,
                    // Generated resources
                    sourceSets.gwt.output.resourcesDir,
                    // Generated classes
                    sourceSets.gwt.output.classesDir,
                    // Dependecies
                    sourceSets.gwt.compileClasspath,

                    // Souceset folders of the current module
                    sourceSets[moduleName+'_gwt'].java.srcDirs,
                    sourceSets[moduleName+'_gwt'].output.resourcesDir,
                    sourceSets[moduleName+'_gwt'].output.classesDir
                ]
            }


            args =
                    [
                        gwtModule,
                        // Your GWT module
                        '-war',
                        buildDir,
                        '-logLevel',
                        'ERROR',
                        '-localWorkers',
                        '2',
                        '-style',
                        'obfuscated',
                        '-extra',
                        extraDir,
                        '-strict'
                        // '-draftCompile' // Speeds up compile with 25%
                    ]
            jvmArgs = [ '-Dgwt.jjs.permutationWorkerFactory=com.google.gwt.dev.ThreadedPermutationWorkerFactory']
            

            maxHeapSize = max_heap_size
        }

        tasks["jar_$moduleName"].dependsOn("gwt_$moduleName")
    }
}

tasks.findAll{ task -> task.name.startsWith('dist_')}.each{ dist_task ->
    if (dist_task.requiresJar.toBoolean()){
        dist_task.dependencies.each{ dep ->
            def depCompileName = 'compile'+dep.replaceAll('\\.','')+'java'
            tasks.findAll{ comp_task -> comp_task.name.toLowerCase().equals(depCompileName)}.each {comp_task ->
                sourceSets[dist_task.moduleName].compileClasspath += files("${project.buildDir}/classes/$dep") { builtBy comp_task.name }
                // we need to manipulate the classpath of the java doc task specifically
                tasks['javadoc_'+dist_task.moduleName].classpath += files("${project.buildDir}/classes/$dep") { builtBy comp_task.name }
            }
        }
        tasks['jar_'+dist_task.moduleName].dependsOn{
            tasks.findAll{ comp_task -> comp_task.name.toLowerCase().equals('compile'+dist_task.moduleName.replaceAll('\\.','')+'java')}
        }
        tasks['javadoc_'+dist_task.moduleName].dependsOn{
            tasks.findAll{ comp_task -> comp_task.name.toLowerCase().equals('compile'+dist_task.moduleName.replaceAll('\\.','')+'java')}
        }
    }
}

task allModules(dependsOn: tasks.matching{ Task task -> task.name.startsWith('dist_')||task.name.startsWith('jar_')}) << {
    println '======================================================'
    println 'Done building modules'
    println '======================================================'
}

task war (dependsOn: [setupJar, widgetSetJar, allModules], type: Zip){

    archiveName 'opencms.war'
    baseName 'opencms-webapp'
    extension 'war'

	from ("${projectDir}/webapp"){
        // exclude the database drivers
        exclude '**/setup/**/*.jar'
        exclude '**/update/**'
        exclude '**/workplaceThemes/**'
        exclude '**/classes/META-INF/**'
        exclude '**/solr/**'
        exclude '**/spellcheck/**'
        exclude '**/*.html'
        exclude '**/*.properties'
        exclude '**/*.txt'
        exclude '**/*.xml'
    }
    
	// copy SOLR config files separately to avoid the replace filter corrupting them
    from ("${projectDir}/webapp/WEB-INF/solr"){
        into '/WEB-INF/solr/'
    }
    
    from ("${projectDir}/webapp"){
        exclude '**/update/**'
        exclude '**/workplaceThemes/**'
        exclude '**/solr/**'
        exclude '**/spellcheck/**'
        include '**/*.html'
        include '**/*.properties'
        include '**/*.txt'
        include '**/*.xml'
        filter(ReplaceTokens, tokens: [
            OPENCMS_VERSION_NUMBER: version,
            OPENCMS_VERSION: product_name+' '+version,
            DRIVERS_VFS: drivers_vfs,
            DRIVERS_PROJECT: drivers_project,
            DRIVERS_USER: drivers_user,
            DRIVERS_HISTORY: drivers_history,
            DRIVERS_CONFIGURATION: drivers_configuration,
            DRIVERS_SUBSCRIPTION: drivers_subscription,
            ADDITIONAL_DB_POOLS: db_additional_pools,
            OPENCMS_CONFIGURATION: opencms_configuration,
            RUNTIME_INFO: system_runtimeinfo
        ])
    }
    
    from ("${project.buildDir}/libs") {
        include '*.jar'
        exclude 'opencms-gwt*.jar'
        exclude 'opencms-test*.jar'
        exclude '*-sources.jar'
        exclude '*-javadoc.jar'
        into '/WEB-INF/lib'
    }
    
    into ('/WEB-INF/lib') { from configurations.distribution }
    
    fileTree('webapp/setup/database'){ include '**/*.jar'}.each {File driverJar ->
        // copy the database drivers into the lib folder
        from (driverJar.path) { into '/WEB-INF/lib' }
    }
    
    from (modulesDistsDir) { into '/WEB-INF/packages/modules' }

    from("${projectDir}/lib/jni") { into "/WEB-INF/lib/jni" }
}

task updater (dependsOn: [setupJar, widgetSetJar, allModules], type: Zip){
    baseName 'opencms-upgrade-to'
    ext.preserveLibModules = preserveLibModules
    
    // only process the text files with the ReplaceTokens filter, copy everything else in a second from clause 
    from ("${projectDir}/webapp"){
        include 'update/**/*.ori'
        include 'update/**/*.html'
        include 'update/**/*.xml'
        include 'update/**/*.txt'
        include 'update/**/*.jsp'
        exclude 'update/readme.txt'
        exclude 'update/empty.jar'
        filter(ReplaceTokens, tokens: [
            OPENCMS_VERSION_NUMBER: version,
            OPENCMS_VERSION: product_name+' '+version,
            PRESERVE_LIB_MODULES: preserveLibModules
        ])
    }
    
    from ("${projectDir}/webapp"){
        include 'update/**'
        exclude 'update/**/*.ori'
        exclude 'update/**/*.html'
        exclude 'update/**/*.xml'
        exclude 'update/**/*.txt'
        exclude 'update/**/*.jsp'
        exclude 'update/readme.txt'
        exclude 'update/empty.jar'
    }

    from ("${projectDir}/webapp/WEB-INF/solr"){
        into '/WEB-INF/solr-update/'
        include 'conf/schema.xml'
        include 'conf/solrconfig.xml'
        include 'solr.xml'
    }
    from ("${projectDir}/webapp/WEB-INF/solr") {
        into '/WEB-INF/solr-complete/'
        include '*'
        include '**/*'
    }
    
    from ("${projectDir}/webapp/update/readme.txt"){
        filter(ReplaceTokens, tokens: [
            OPENCMS_VERSION_NUMBER: version,
            OPENCMS_VERSION: product_name+' '+version
        ])
    }
    from ("${projectDir}/webapp/WEB-INF"){
        into '/WEB-INF'
        include 'opencms.tld'
        include 'web.xml'
        include 'cmsshell.sh'
        include 'classes/META-INF/persistence.xml'
        include 'classes/ehcache.xml'
        include 'classes/repository.properties'
        include 'sun-jaxws.xml'
        include 'wsdl/**'
        include 'spellcheck/**'
    }
    from ("${projectDir}/webapp/resources"){
        into '/resources'
        include '*'
        include '**/*'
    }
    from ("${project.buildDir}/libs") {
        include 'opencms.jar'
        include 'opencms-setup.jar'
        include 'opencms-widgetset.jar'
        into '/WEB-INF/lib'
    }
    into ('/WEB-INF/lib') { from configurations.distribution }
    tasks.findAll{dist_task -> dist_task.name.startsWith('dist_') && !(updater_excluded_modules.split(',').contains(dist_task.moduleName))}.each{ dist_task ->
        from ("${modulesDistsDir}/${dist_task.archiveName}"){ into '/update/modules' }
        if (dist_task.requiresJar.toBoolean()){
            from ("${project.buildDir}/libs/${dist_task.moduleName}.jar"){ into 'WEB-INF/lib' }
        }
    }
    fileTree('webapp/setup/database'){ include '**/*.jar'}.each {File driverJar ->
        // copy the database drivers into the lib folder
        from (driverJar.path) { into '/WEB-INF/lib' }
    }
    Properties buildProps = new Properties()
    buildProps.load(new FileInputStream("${projectDir}/build-default.properties"))
    buildProps['updater.jars.remove'].split(',').each{ jar ->
        from("${projectDir}/webapp/update/empty.jar"){
            into '/WEB-INF/lib'
            rename('empty\\.jar', jar)
        }
    }
}

updater.dependsOn(tasks.matching{dist_task -> dist_task.name.startsWith('dist_') && !(updater_excluded_modules.split(',').contains(dist_task.moduleName))})

task bindist (dependsOn: war, type: Zip){
    baseName 'opencms'
    from "${project.buildDir}/distributions/opencms.war"
    from(projectDir) {
        include 'install.html'
        include 'license.txt'
        include 'history.txt'
        filter(ReplaceTokens, tokens: [
            OPENCMS_VERSION_NUMBER: version,
            OPENCMS_VERSION: product_name+' '+version
        ])
    }
}


test {
    classpath += fileTree('webapp/setup/database'){ include '**/*.jar' }
    classpath += files("$buildDir/classes/setup") { builtBy 'setupClasses' }
    classpath += files("$buildDir/classes/modules") { builtBy 'modulesClasses' }
    classpath += files("$buildDir/classes/gwt") { builtBy 'gwtClasses' }
    useJUnit()
    include 'org/opencms/test/AllTests*'
    // important: exclude all anonymous classes
    exclude '**/*$*.class'
    scanForTestClasses false
    testClassesDir = sourceSets.test.output.classesDir
    systemProperties['test.data.path'] = "${projectDir}/test/data"
    systemProperties['test.webapp.path'] = "${projectDir}/webapp"
    systemProperties['test.build.folder'] =sourceSets.test.output.resourcesDir
    maxHeapSize = max_heap_size
    jvmArgs '-XX:MaxPermSize=256m'
    testLogging.showStandardStreams = true
    ignoreFailures true
}

task testSingle(type: Test, dependsOn: [compileTestJava]) {
    description "Runs a specified test case set like this: -PtestCaseToRun=org/opencms/main/TestCmsSystemInfo*"
    classpath += fileTree('webapp/setup/database'){ include '**/*.jar' }
    classpath += files("$buildDir/classes/setup") { builtBy 'setupClasses' }
    classpath += files("$buildDir/classes/modules") { builtBy 'modulesClasses' }
    classpath += files("$buildDir/classes/gwt") { builtBy 'gwtClasses' }
    useJUnit()
    if (!project.hasProperty('testCaseToRun')){
        ext {
            testCaseToRun='org/opencms/main/TestCmsSystemInfo*'
        }
    }
    doFirst{
        println "Running test case : ${testCaseToRun}"
    }    
    include testCaseToRun
    // important: exclude all anonymous classes
    exclude '**/*$*.class'
    scanForTestClasses false
    testClassesDir = sourceSets.test.output.classesDir
    systemProperties['test.data.path'] = "${projectDir}/test/data"
    systemProperties['test.webapp.path'] = "${projectDir}/webapp"
    systemProperties['test.build.folder'] =sourceSets.test.output.resourcesDir
    maxHeapSize = max_heap_size
    jvmArgs '-XX:MaxPermSize=256m'
    testLogging.showStandardStreams = true
    ignoreFailures true
}

task testJar(dependsOn: compileTestJava, type: Jar) {
    from sourceSets.test.output
    baseName 'opencms-test'
    exclude '**/.gitignore'
    exclude '**/*.java'
}

task javadocTest(type: Javadoc, dependsOn: jar) {
    source = sourceSets.test.allJava
    classpath = project.sourceSets.test.compileClasspath
    destinationDir = file("${buildDir}/docs/javadocTest")
}

task javadocJarTest(type: Jar, dependsOn: javadocTest) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadocTest"
    baseName 'opencms-test'
}

task sourcesJarTest(type: Jar) {
    from sourceSets.test.allSource
    classifier = 'sources'
    baseName 'opencms-test'
}


task testGwt(type: Test, dependsOn: [compileTestGwtJava]) {
    classpath=sourceSets.testGwt.runtimeClasspath
    classpath += files("${projectDir}/src")
    classpath += files("${projectDir}/src-gwt")
    classpath += files("${projectDir}/test-gwt")
    useJUnit()
    include 'org/opencms/client/test/AllTests*'
    // important: exclude all anonymous classes
    exclude '**/*$*.class'
    scanForTestClasses false
    testClassesDir = sourceSets.testGwt.output.classesDir
    systemProperties['gwt.args'] = "-logLevel WARN"
    systemProperties['java.awt.headless'] = "true"
    maxHeapSize = max_heap_size
    jvmArgs '-XX:MaxPermSize=256m'
    testLogging.showStandardStreams = true
    ignoreFailures true
}

artifacts {
    archives jar
    archives sourcesJar
    
    if (project.hasProperty('publish_repository')){
        // do not include the war if only running the install task, it takes ages to build
        archives war
        archives widgetSetJar
    }
    archives setupJar
    archives sourcesJarSetup
    
    archives gwtJar
    archives sourcesJarGwt
    
    archives testJar
    archives sourcesJarTest
    
    tasks.each{ task ->
        if (task.name.startsWith("jar_org.opencms.")){
        	def moduleName=task.baseName
            archives task
            archives tasks["sourcesJar_$moduleName"]
        }
    }
    // in case the property 'skip_javadoc' is set to 'true', javadoc generation is skipped - this is supposed to speed up build time for CI
    if (!(project.hasProperty('skip_javadoc') && skip_javadoc.toBoolean())){
    	archives javadocJar
    	archives javadocJarSetup
    	archives javadocJarGwt
    	archives javadocJarTest
    	tasks.each{ task ->
        if (task.name.startsWith("jar_org.opencms.")){
        	def moduleName=task.baseName
            archives tasks["javadocJar_$moduleName"]
        }
    }
    }
}

signing {
    required {gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}



if (project.hasProperty('publish_repository')){
    uploadArchives {
        it.dependsOn(signArchives)
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: publish_repository) {
                    authentication(userName: publish_user, password: publish_password)
                }
                addFilter('core'){artifact, file ->
                    artifact.name.startsWith('opencms-core')
                }
                pom('core').project {
                    name product_name
                    description product_description
                    packaging 'jar'
                    groupId 'org.opencms'
                    url 'http://www.opencms.org'
                    scm {
                        url 'scm:git@github.com:alkacon/opencms-core.git'
                        connection 'scm:git@github.com:alkacon/opencms-core.git'
                        developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                    }
                    repositories { 
                    	repository{
                    		id 'vaadin-addons'
    						url 'http://maven.vaadin.com/vaadin-addons'
    					}
    					repository {
    						id 'restlet'
    						url 'http://maven.restlet.org'
    					}
    				}
                    licenses {
                        license {
                            name 'GNU Lesser General Public License'
                            url 'http://www.gnu.org/licenses/lgpl.html'
                            distribution 'repo'
                        }
                    }
                    organization {
                        name 'Alkacon Software'
                        url 'http://www.alkacon.com'
                    }
                    developers {
                        developer {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                    }
                }
                pom('core').scopeMappings.addMapping(1, configurations.distribution, 'runtime')
                // all dependencies are non transitive, as gradle is ignoring this setting when creating the pom, we have to manipulate the pom
                pom('core').whenConfigured {pom ->
                    pom.dependencies.each { dependency ->
                        // to avoid class loading problems
                        def exclusion = pom.model.class.classLoader.loadClass("org.apache.maven.model.Exclusion").newInstance();
                        exclusion.setGroupId('*')
                        exclusion.setArtifactId('*')
                        dependency.addExclusion(exclusion)

                    }
                }
                addFilter('war'){artifact, file ->
                    artifact.name.startsWith('opencms-webapp')
                }
                pom('war').project {
                    name product_name
                    description product_description
                    packaging 'war'
                    groupId 'org.opencms'
                    url 'http://www.opencms.org'
                    scm {
                        url 'scm:git@github.com:alkacon/opencms-core.git'
                        connection 'scm:git@github.com:alkacon/opencms-core.git'
                        developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                    }
                    licenses {
                        license {
                            name 'GNU Lesser General Public License'
                            url 'http://www.gnu.org/licenses/lgpl.html'
                            distribution 'repo'
                        }
                    }
                    organization {
                        name 'Alkacon Software'
                        url 'http://www.alkacon.com'
                    }
                    developers {
                        developer {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                    }
                }
                'setup,gwt,widgetset,test'.split(",").each { groupName ->
                    addFilter(groupName){artifact, file ->
                        artifact.name.startsWith("opencms-$groupName")
                    }
                    pom(groupName).project {
                        name product_name
                        description product_description
                        packaging 'jar'
                        groupId 'org.opencms'
                        url 'http://www.opencms.org'
                        scm {
                            url 'scm:git@github.com:alkacon/opencms-core.git'
                            connection 'scm:git@github.com:alkacon/opencms-core.git'
                            developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                        }
                        licenses {
                            license {
                                name 'GNU Lesser General Public License'
                                url 'http://www.gnu.org/licenses/lgpl.html'
                                distribution 'repo'
                            }
                        }
                        organization {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                        developers {
                            developer {
                                name 'Alkacon Software'
                                url 'http://www.alkacon.com'
                            }
                        }
                    }
                    pom(groupName).whenConfigured {pom ->
                        // the setup jar depends on opencms-core, override anything else
                        pom.dependencies.clear()
                        // to avoid class loading problems
                        def dependency = pom.model.class.classLoader.loadClass("org.apache.maven.model.Dependency").newInstance();
                        dependency.setGroupId("org.opencms")
                        dependency.setArtifactId("opencms-core")
                        dependency.setVersion(version)
                        pom.dependencies.add(dependency)
                    }
                }

                artifacts.each{ arch ->
                    def filterName=arch.name
                    if (filterName.startsWith("org.opencms")){
                        filterName=filterName.substring(0, filterName.lastIndexOf(".jar"))
                        addFilter(filterName){artifact, file ->
                            artifact.name == filterName || artifact.name.startsWith(filterName + '-')
                        }
                        pom(filterName).project {
                            name product_name
                            description product_description
                            packaging 'jar'
                            groupId 'org.opencms'
                            url 'http://www.opencms.org'
                            scm {
                                url 'scm:git@github.com:alkacon/opencms-core.git'
                                connection 'scm:git@github.com:alkacon/opencms-core.git'
                                developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                            }
                            licenses {
                                license {
                                    name 'GNU Lesser General Public License'
                                    url 'http://www.gnu.org/licenses/lgpl.html'
                                    distribution 'repo'
                                }
                            }
                            organization {
                                name 'Alkacon Software'
                                url 'http://www.alkacon.com'
                            }
                            developers {
                                developer {
                                    name 'Alkacon Software'
                                    url 'http://www.alkacon.com'
                                }
                            }
                        }
                        pom(filterName).whenConfigured {pom ->
                            // the jars depend on opencms-core, override anything else
                            pom.dependencies.clear()
                            // to avoid class loading problems
                            def dependency = pom.model.class.classLoader.loadClass("org.apache.maven.model.Dependency").newInstance();
                            dependency.setGroupId("org.opencms")
                            dependency.setArtifactId("opencms-core")
                            dependency.setVersion(version)
                            pom.dependencies.add(dependency)
                        }
                    }
                }
            }
        }
    }
}

install {
	doFirst {
		println "Installing artifact for version $version"
	}

    repositories {
        mavenInstaller {
            addFilter('core'){artifact, file ->
                artifact.name.startsWith('opencms-core')
            }
            pom('core').project {
                name product_name
                description product_description
                packaging 'jar'
                groupId 'org.opencms'
                url 'http://www.opencms.org'
                scm {
                    url 'scm:git@github.com:alkacon/opencms-core.git'
                    connection 'scm:git@github.com:alkacon/opencms-core.git'
                    developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                }
                repositories { 
                   	repository{
                   		id 'vaadin-addons'
    					url 'http://maven.vaadin.com/vaadin-addons'
    				}
    				repository {
    					id 'restlet'
    					url 'http://maven.restlet.org'
    				}
    			}
                licenses {
                    license {
                        name 'GNU Lesser General Public License'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                        distribution 'repo'
                    }
                }
                organization {
                    name 'Alkacon Software'
                    url 'http://www.alkacon.com'
                }
                developers {
                    developer {
                        name 'Alkacon Software'
                        url 'http://www.alkacon.com'
                    }
                }
            }
            pom('core').scopeMappings.addMapping(1, configurations.distribution, 'runtime')
            // all dependencies are non transitive, as gradle is ignoring this setting when creating the pom, we have to manipulate the pom
            pom('core').whenConfigured {pom ->
                pom.dependencies.each { dependency ->
                    // to avoid class loading problems
                    def exclusion = pom.model.class.classLoader.loadClass("org.apache.maven.model.Exclusion").newInstance();
                    exclusion.setGroupId('*')
                    exclusion.setArtifactId('*')
                    dependency.addExclusion(exclusion)

                }
            }
            addFilter('war'){artifact, file ->
                artifact.name.startsWith('opencms-webapp')
            }
            pom('war').project {
                name product_name
                description product_description
                packaging 'war'
                groupId 'org.opencms'
                url 'http://www.opencms.org'
                scm {
                    url 'scm:git@github.com:alkacon/opencms-core.git'
                    connection 'scm:git@github.com:alkacon/opencms-core.git'
                    developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                }
                licenses {
                    license {
                        name 'GNU Lesser General Public License'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                        distribution 'repo'
                    }
                }
                organization {
                    name 'Alkacon Software'
                    url 'http://www.alkacon.com'
                }
                developers {
                    developer {
                        name 'Alkacon Software'
                        url 'http://www.alkacon.com'
                    }
                }
            }
            'setup,gwt,test,widgetset'.split(",").each { groupName ->
                addFilter(groupName){artifact, file ->
                    artifact.name.startsWith("opencms-$groupName")
                }
                pom(groupName).project {
                    name product_name
                    description product_description
                    packaging 'jar'
                    groupId 'org.opencms'
                    url 'http://www.opencms.org'
                    scm {
                        url 'scm:git@github.com:alkacon/opencms-core.git'
                        connection 'scm:git@github.com:alkacon/opencms-core.git'
                        developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                    }
                    licenses {
                        license {
                            name 'GNU Lesser General Public License'
                            url 'http://www.gnu.org/licenses/lgpl.html'
                            distribution 'repo'
                        }
                    }
                    organization {
                        name 'Alkacon Software'
                        url 'http://www.alkacon.com'
                    }
                    developers {
                        developer {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                    }
                }
                pom(groupName).whenConfigured {pom ->
                    // the setup jar depends on opencms-core, override anything else
                    pom.dependencies.clear()
                    // to avoid class loading problems
                    def dependency = pom.model.class.classLoader.loadClass("org.apache.maven.model.Dependency").newInstance();
                    dependency.setGroupId("org.opencms")
                    dependency.setArtifactId("opencms-core")
                    dependency.setVersion(version)
                    pom.dependencies.add(dependency)
                }
            }
            artifacts.each{ arch ->
                def filterName=arch.name
                if (filterName.startsWith("org.opencms")){
                    filterName=filterName.substring(0, filterName.lastIndexOf(".jar"))
                    addFilter(filterName){artifact, file ->
                    	artifact.name == filterName || artifact.name.startsWith(filterName + '-')
                    }
                    
                    pom(filterName).project {
                    	name product_name
                        description product_description
                        packaging 'jar'
                        groupId 'org.opencms'
                        url 'http://www.opencms.org'
                        scm {
                            url 'scm:git@github.com:alkacon/opencms-core.git'
                            connection 'scm:git@github.com:alkacon/opencms-core.git'
                            developerConnection 'scm:git@github.com:alkacon/opencms-core.git'
                        }
                        licenses {
                            license {
                                name 'GNU Lesser General Public License'
                                url 'http://www.gnu.org/licenses/lgpl.html'
                                distribution 'repo'
                            }
                        }
                        organization {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                        developers {
                            developer {
                                name 'Alkacon Software'
                                url 'http://www.alkacon.com'
                            }
                        }
                    }
                    pom(filterName).whenConfigured {pom ->
                        // the jars depend on opencms-core, override anything else
                        pom.dependencies.clear()
                        // to avoid class loading problems
                        def dependency = pom.model.class.classLoader.loadClass("org.apache.maven.model.Dependency").newInstance();
                        dependency.setGroupId("org.opencms")
                        dependency.setArtifactId("opencms-core")
                        dependency.setVersion(version)
                        pom.dependencies.add(dependency)
                    }
                }
            }
        }
    }
}

// fixed issue with libraries containing both .java and .class files
tasks.withType(JavaCompile) {
    options.compilerArgs += ["-sourcepath", ""]
    options.encoding='UTF-8'
}
tasks.withType(Javadoc) {
    options.addStringOption("sourcepath", "")
    if (JavaVersion.current().isJava8Compatible()) {
    	options.addStringOption("Xdoclint:none", "-quiet")
    }
}
